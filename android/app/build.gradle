plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def keystorePropertiesFile = rootProject.file("release.properties")
def keystoreProperties = new Properties()

if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    namespace "com.fafutech.ifafu"
    compileSdkVersion flutter.compileSdkVersion
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.fafutech.ifafu"
        minSdkVersion 19
        compileSdkVersion 34
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        multiDexEnabled true
    }

    signingConfigs {
        if (keystorePropertiesFile.exists() && rootProject.file(keystoreProperties['storeFile']).exists()) {
            release {
                storeFile rootProject.file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"

            manifestPlaceholders = [
                app_name      : "iFAFU.debug",
                app_icon      : "@mipmap/ic_launcher_debug",
                app_icon_round: "@mipmap/ic_launcher_debug_round",
            ]
        }

        profile {
            applicationIdSuffix ".profile"

            manifestPlaceholders = [
                app_name      : "iFAFU.profile",
                app_icon      : "@mipmap/ic_launcher",
                app_icon_round: "@mipmap/ic_launcher_round",
            ]
        }

        release {

            manifestPlaceholders = [
                app_name      : "iFAFU",
                app_icon      : "@mipmap/ic_launcher",
                app_icon_round: "@mipmap/ic_launcher_round",
            ]

            if (!keystorePropertiesFile.exists()) {
                println 'Keystore properties not found, use debug key'
                signingConfig signingConfigs.debug
            } else if (!rootProject.file(keystoreProperties['storeFile']).exists()){
                println "Key ${keystoreProperties['storeFile']} not found, use debug key"
                signingConfig signingConfigs.debug
            } else {
                println 'Use release key'
                signingConfig signingConfigs.release
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "androidx.multidex:multidex:2.0.1"
    implementation 'com.google.android.material:material:1.10.0'
}
